<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:myComp="components.*"
                creationComplete="initializeApplication()">
    <mx:Script><![CDATA[
        import events.BookSelectedEvent;
        import events.MainNavigationEvent;
        import events.RemoteExceptionEvent;
        import events.AuthenticationEvent;
        import services.SecurityService;
        import mx.controls.Alert;

        [Bindable]
        private var authenticationHelper:SecurityService = new SecurityService();

        /**
         * Mainly used to set some listeners and parameters on application scope, it also calls the function
         * to present the form to enter credentials for authentication of a user. The successfull authentication
         * event is handled on an application scope, the listener for this event is added here.
         */
        private function initializeApplication():void {
            Application.application.addEventListener(AuthenticationEvent.AUTHENTICATION, handleAuthenticationEvent);
            Application.application.addEventListener(RemoteExceptionEvent.REMOTE_EXCEPTION, handleRemoteExceptionEvent);
            authenticateUser();
        }

        /**
         * Used to present the form where a user can enter his credentials and ask for authentication
         */
        private function authenticateUser():void {
            showRightUIComponent('authentication');
        }

        /**
         * Called when a NavigationEvent is dispatched, based on the event, the right UIComponent is created
         * and presented in the main panel.
         * @param event NavigationEvent thrown when a user clicks a menu item (by example)
         */
        private function handleNavigationEvent(event:MainNavigationEvent):void {
            showRightUIComponent(event.clickedItem);
        }

        /**
         * This method is called after a succesfull authentication request. It initializes the application
         * for normal use by adding navigation and other global items.
         * @param event
         */
        private function handleAuthenticationEvent(event:AuthenticationEvent):void {
            mainNavigation.initMyComponent();
            mainNavigation.addEventListener(MainNavigationEvent.SELECT_ITEM, handleNavigationEvent);
            filteredBooks.addEventListener(BookSelectedEvent.SELECT_BOOK, handleBookSelectedEvent)
            showRightUIComponent("default");
        }

        /**
         * Used to handle a BookSelectedEvent. Result must be the book form with the data of the selected book.
         * @param event : BookSelectedEvent
         * @return void
         */
        private function handleBookSelectedEvent(event:BookSelectedEvent):void {
            bookForm.book = event.selectedBook;
            showRightUIComponent('selectedbook');
        }

        /**
         * Called when a remote exception is catched, for now we only show the error message
         * @param event : RemoteExceptionEvent
         * @return void
         */
        private function handleRemoteExceptionEvent(event:RemoteExceptionEvent):void {
            Alert.show(event.message);
        }

        /**
         * Used to select the item o show from the stack of items
         * @param itemToShow : String
         * @return void
         */
        private function showRightUIComponent(itemToShow:String):void {
            switch (itemToShow) {
                case 'authentication':
                    mainContentViewStack.selectedChild = authenticationForm;
                    break;
                case 'allbooks' :
                    mainContentViewStack.selectedChild = filteredBooks;
                    break;
                case 'selectedbook' :
                    mainContentViewStack.selectedChild = bookForm;
                    break;
                case 'newbook' :
                    bookForm.clearForm();
                    mainContentViewStack.selectedChild = bookForm;
                    break;
                default:
                    mainContentViewStack.selectedChild = welcomeMessage;
            }
        }

        ]]>
    </mx:Script>

    <mx:ApplicationControlBar dock="true" id="menuDock">
        <myComp:MainNavigationComponent id="mainNavigation"/>
    </mx:ApplicationControlBar>

    <mx:Panel id="myMainContentPanel" title="My Books demo app" status="Welcome" width="90%" height="90%">
        <mx:ViewStack id="mainContentViewStack" width="100%" height="100%">
            <myComp:AuthenticationForm id="authenticationForm" securityService="{authenticationHelper}"/>
            <myComp:FilteredBooks id="filteredBooks"/>
            <myComp:BookForm id="bookForm"/>
            <mx:HBox id="welcomeMessage">
                <mx:Label id="welcomeLabel" text="Welcome"/>
            </mx:HBox>
        </mx:ViewStack>
    </mx:Panel>

</mx:Application>